# Maintainer: Chris Magyar <c.magyar.ec@gmail.com>

pkgname=ramroot-dev
_gitname=ramroot
pkgver=git
pkgrel=1
pkgdesc='Load root filesystem to zram in early userspace'
arch=(any)
url="https://github.com/arcmags/$_gitname"
license=(GPL-3.0-or-later)
depends=(mkinitcpio)
makedepends=(git)
conflicts=(ramroot ramroot-git)
provides=(ramroot)
backup=(etc/ramroot.conf etc/ramroot.z/etc/issue.d/ramroot.issue)
source=("git+$url.git")
sha256sums=(SKIP)

prepare() {
    cd "$_gitname"
    git checkout -b dev origin/dev
}

pkgver() {
    cd "$_gitname"
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "$_gitname"
    make DESTDIR="$pkgdir" install
}
