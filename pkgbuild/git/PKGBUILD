# Maintainer: Chris Magyar <c.magyar.ec@gmail.com>

pkgname=ramroot-git
_gitname=ramroot
pkgver=git
pkgrel=1
pkgdesc='Load root filesystem to zram in early userspace'
arch=(any)
url="https://github.com/arcmags/$_gitname"
license=(GPL-3.0-or-later)
depends=(mkinitcpio)
makedepends=(git)
conflicts=(ramroot)
provides=(ramroot)
backup=(etc/ramroot.conf etc/ramroot.z/etc/issue.d/ramroot.issue)
source=("git+$url.git")
sha256sums=(SKIP)

pkgver() {
    cd "$_gitname"
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "$_gitname"
    make DESTDIR="$pkgdir" install
}
